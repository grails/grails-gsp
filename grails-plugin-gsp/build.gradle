ext {
    isTravisBuild = System.getenv().get("TRAVIS") == 'true'
}
configurations.all {
    resolutionStrategy.dependencySubstitution.all { DependencySubstitution dependency ->
        if (dependency.requested instanceof ModuleComponentSelector) {
            if (group == 'org.grails' || group == 'org.grails.plugins') {
                def targetProject = findProject(":${dependency.requested.module}")
                if (targetProject != null) {
                    dependency.useTarget targetProject
                }
            }
        }
    }
}
dependencies {
    compileOnly "javax.servlet:javax.servlet-api:$servletApiVersion"
    api project(":grails-web-gsp-taglib")


    runtimeOnly(project(":grails-web-jsp"))
    api "commons-lang:commons-lang:2.6"
    api "org.grails:grails-plugin-codecs:$grailsVersion"
    astCompile "org.grails:grails-web:$grailsVersion"    
    astCompile "org.grails:grails-plugin-controllers:$grailsVersion"
    
    testImplementation 'org.grails:grails-web-testing-support:2.0.0.RC1', {
        exclude module:'async'
    }
    // testImplementation 'org.grails:grails-async:4.0.0.BUILD-SNAPSHOT'
    testImplementation 'org.grails:grails-testing-support:2.2.0.BUILD-SNAPSHOT', {
        exclude module:'async'
    }
    testImplementation 'org.grails:grails-gorm-testing-support:2.0.0.RC1', {
        exclude module:'async'
    }

    testRuntimeOnly "org.grails.plugins:async"
    testImplementation "javax.servlet.jsp:jsp-api:${jspApiVersion}"
    testImplementation "org.springframework:spring-test:$springVersion"

    testImplementation "taglibs:standard:${jstlVersion}"
    testImplementation "javax.servlet:jstl:${jstlVersion}"

    testRuntimeOnly "org.grails:grails-plugin-url-mappings:$grailsVersion"
}
// disable main class
bootJar {
    mainClassName = 'dummy.Application'
}
findMainClass.onlyIf { false }
test {
    if (isTravisBuild) {
        maxParallelForks = 1
        forkEvery = 10
    }
    else {
        maxParallelForks = 4
        forkEvery = 20
    }

    jvmArgs = ['-Xmx1536M']
    afterSuite {
        System.out.print('.')
        System.out.flush()
    }
}
