buildscript {
    repositories {
        mavenCentral()
        maven { url "https://repo.grails.org/grails/core" }
        maven { url "https://plugins.gradle.org/m2/" }
    }
    dependencies {
        classpath "org.grails:grails-gradle-plugin:$grailsGradlePluginVersion"
        classpath "org.grails:grails-docs:${project.ext.properties.grailsDocsVersion ?: grailsVersion}"
        classpath "io.github.gradle-nexus:publish-plugin:1.3.0"
    }
}

repositories {
    mavenLocal()
    maven { url "https://repo.grails.org/grails/core" }
}

version project.projectVersion

ext {
    groovyVersion = System.getenv('CI_GROOVY_VERSION') ?: project.groovyVersion
    isCiBuild = System.getenv().get("TRAVIS") == 'true' || (System.getenv().get("CI") as Boolean)
    commonBuild = 'https://raw.githubusercontent.com/grails/grails-common-build/master'
}

apply plugin:'idea'

subprojects {

    version project.projectVersion

    ext {
        userOrg = "grails"
        isGrailsPlugin = name.startsWith('grails-plugin')
        isBuildSnapshot = version.toString().endsWith("-SNAPSHOT")
        groovyVersion = System.getenv('CI_GROOVY_VERSION') ?: project.groovyVersion
    }

    if (isGrailsPlugin) {
        group "org.grails.plugins"
    } else {
        group "org.grails"
    }

    repositories {
        mavenLocal()
        maven { url "https://repo.grails.org/grails/core" }
    }

    if (it.name.startsWith("examples")) {
        apply plugin: "org.grails.grails-web"
    } else if (isGrailsPlugin) {
        apply plugin: "org.grails.grails-plugin"
    } else {
        apply from: "${commonBuild}/common-project.gradle"

        dependencies {
            documentation "org.codehaus.groovy:groovy-dateutil:$groovyVersion"
        }
    }

    apply plugin:'idea'
    apply plugin: 'java-library'

    java {
        sourceCompatibility = JavaVersion.VERSION_11
        targetCompatibility = JavaVersion.VERSION_11
    }

    configurations.all {
        resolutionStrategy.eachDependency { DependencyResolveDetails details ->
            if (details.requested.group == "org.spockframework") {
                details.useVersion(spockVersion)
            } else if (details.requested.group == "org.junit.jupiter") {
                details.useVersion(junitJupiterVersion)
            } else if (details.requested.group == "org.junit.platform") {
                details.useVersion(junitPlatformVerison)
            }
        }
    }

    dependencies {
        compileOnly "javax.servlet:javax.servlet-api:$servletApiVersion"

        testImplementation "javax.servlet:javax.servlet-api:$servletApiVersion"
        testImplementation "org.codehaus.groovy:groovy-test-junit5:${groovyVersion}"
        testImplementation "org.junit.jupiter:junit-jupiter-api:${junitJupiterVersion}"
        testImplementation "org.junit.platform:junit-platform-runner:${junitPlatformVerison}"
        testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:${junitJupiterVersion}"
    }

    tasks.withType(Test) {
        useJUnitPlatform()
        testLogging {
            showStandardStreams = true
            exceptionFormat = 'full'
        }
        if (isCiBuild) {
            maxParallelForks = 2
        } else {
            maxParallelForks = 4
        }
    }
}

apply from: "${commonBuild}/common-docs.gradle"
apply from: "${commonBuild}/common-publishing.gradle"

tasks.named('publishGuide') {
    // Override value defined in grails/grails-common-build/common-docs.gradle
    it.properties['commandLineRef'] = "https://docs.grails.org/$grailsVersion/ref/Command%20Line"
    it.properties['controllersRef'] = "https://docs.grails.org/$grailsVersion/ref/Controllers"
    it.properties['grailsapi']      = "https://docs.grails.org/$grailsVersion/api/"
    it.properties['grailsdocs']     = "https://docs.grails.org/$grailsVersion/"
    it.properties['groovyapi']      = "https://docs.groovy-lang.org/$groovyVersion/html/gapi/"
    it.properties['groovyjdk']      = "https://docs.groovy-lang.org/$groovyVersion/html/groovy-jdk/"
    it.properties['javase']         = 'https://docs.oracle.com/en/java/javase/11/docs/api/'
}

boolean isNonStable(String version) {
    version ==~ /(?i).+(-|\.?)(b|M|RC|Dev)\d?.*/ ||
            ['alpha', 'beta', 'milestone', 'rc', 'cr', 'm', 'preview', 'b', 'ea'].any { qualifier ->
                version ==~ /(?i).*[.-]$qualifier[.\d-+]*/
            }
}
