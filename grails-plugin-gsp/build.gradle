ext {
    isCiBuild = System.getenv().get("TRAVIS") == 'true' || System.getenv().get("CI") as Boolean
}
configurations.all {
    resolutionStrategy.dependencySubstitution.all { DependencySubstitution dependency ->
        if (dependency.requested instanceof ModuleComponentSelector) {
            if (group == 'org.grails' || group == 'org.grails.plugins') {
                def targetProject = findProject(":${dependency.requested.module}")
                if (targetProject != null) {
                    dependency.useTarget targetProject
                }
            }
        }
    }
}
dependencies {
    compileOnly "jakarta.servlet:jakarta.servlet-api:$servletApiVersion"
    api project(":grails-web-gsp-taglib")

    runtimeOnly(project(":grails-web-jsp"))
    api "commons-lang:commons-lang:2.6"
    api "org.grails:grails-plugin-codecs:$grailsVersion"
    astImplementation "org.grails:grails-web:$grailsVersion"
    astImplementation "org.grails:grails-plugin-controllers:$grailsVersion"

    testImplementation "org.grails:grails-web-testing-support:$testingSupportVersion", {
        exclude module:'async'
    }
    testImplementation "org.grails:grails-testing-support:$testingSupportVersion", {
        exclude module:'async'
    }
    testImplementation "org.grails:grails-gorm-testing-support:$testingSupportVersion", {
        exclude module:'async'
    }

    testRuntimeOnly "org.grails.plugins:async"
    testImplementation "jakarta.annotation:jakarta.annotation-api:$annotationApiVersion"
    testImplementation "jakarta.servlet.jsp:jakarta.servlet.jsp-api:${jspApiVersion}"
    testRuntimeOnly "org.glassfish.web:jakarta.servlet.jsp.jstl:${jstlVersion}"
    testImplementation "jakarta.servlet.jsp.jstl:jakarta.servlet.jsp.jstl-api:${jstlVersion}"

    testRuntimeOnly "org.grails:grails-plugin-url-mappings:$grailsVersion"
}
// disable main class
bootJar {
    mainClass.set('dummy.Application')
}
findMainClass.onlyIf { false }
test {
    if (isCiBuild) {
        maxParallelForks = 1
        forkEvery = 10
    }
    else {
        maxParallelForks = 4
        forkEvery = 20
    }

    jvmArgs = ['-Xmx1536M']
    afterSuite {
        System.out.print('.')
        System.out.flush()
    }
}
