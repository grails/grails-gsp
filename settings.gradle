plugins {
    id "com.gradle.enterprise" version "3.17.1"
    id 'com.gradle.common-custom-user-data-gradle-plugin' version '2.0.1'
}

gradleEnterprise {
    server = 'https://ge.grails.org'
    buildScan {
        publishAlwaysIf(System.getenv('CI') == 'true' )
        publishIfAuthenticated()
        uploadInBackground = System.getenv("CI") == null
        capture {
            taskInputFiles = true
        }
    }
}

buildCache {
    local { enabled = System.getenv('CI') != 'true' }
    remote(gradleEnterprise.buildCache) {
        def isAuthenticated = System.getenv('GRADLE_ENTERPRISE_ACCESS_KEY')
        push = System.getenv('CI') == 'true' && isAuthenticated
        enabled = true
        }
}


rootProject.name = "grails-gsp"

include 'grails-gsp'
include 'grails-plugin-gsp'
include 'grails-plugin-sitemesh2'
include 'grails-taglib'
include 'grails-web-gsp'
include 'grails-web-gsp-taglib'
include 'grails-web-jsp'
include 'grails-web-sitemesh'
include 'grails-web-taglib'
